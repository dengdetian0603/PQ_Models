model {
  # ctrl likelihood
	for (i in 1:N_ctrl) {
    for (j in 1:K) {
      mbs_ctrl[i, j] ~ dbern(bs_fpr[j])
    }
	}

  # case likelihood
  ## new var: I_strata[], N_strata, Beta[,], X_unique[,], D
  for (i in 1:N_case) {
    gs_index[i] ~ dcat(cell_prob[I_strata[i], ])        ##
    for (j in 1:K) {
      mgs[i, j] <- GSmat[gs_index[i], j]
      mbs_case[i, j] ~ dbern(bs_tpr[j]^mgs[i, j] * bs_fpr[j]^(1 - mgs[i, j]))
      mss_case[i, j] ~ dbern(ss_tpr[j]^mgs[i, j] * (mgs[i, j] > 0) + 1.0E-8)
    }
  }
  
  ## 
  for (s in 1:N_strata) {
    cell_prob[s, 1] <- 1
    for (k in 1:J1) {
      for (l in 1:K) {
        Ltheta1[s, k, l] <- Lmat[k, l] * theta1[s, l]
      }
      cell_prob[s, k + 1] <- exp(sum(Ltheta1[s, k, ]) + sum(Utheta2[k, ]))
    }
  }

  for (k in 1:J1) {
    for (l in 1:Umat_ncol) {
      Utheta2[k, l] <- Umat[k, l] * theta2[l]
    }
  }

  for (s in 1:N_strata) {
    for (l in 1:K) {
      theta1[s, l] <- sum(X_unique[s, ] * Beta[l, ])
    }
  }

  # var selection prior
  for (j in 1:Umat_ncol) {
    ind[j] ~ dbern(pind)
    theta2[j] <- theta2_value * ind[j]
  }
  theta2_value ~ dnorm(theta2_mu, tau_theta)
  pind ~ dbeta(pind_a, pind_b)

  # other prior
  for (j in 1:K) {
    Beta[j, 1] ~ dnorm(mu_logit[j], tau_theta * 2)
    ss_tpr[j] ~ dbeta(aa, bb)
    bs_tpr[j] ~ dbeta(cc, dd)
    bs_fpr[j] ~ dbeta(ee, ff)
  }
  for (j in 1:K){
    for (d in 2:D) {
      Beta[j, d] ~ dnorm(0, tau_theta * 2)
    }
  }
}